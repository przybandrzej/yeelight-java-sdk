plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

version = '0.1.2'
sourceCompatibility = 1.8
targetCompatibility = JavaVersion.VERSION_1_8

repositories {
    mavenCentral()
}

ext {
    GSON_V = '2.8.8'
    isReleaseVersion = true
}

dependencies {
    implementation "com.google.code.gson:gson:$GSON_V"
}

tasks.named('jar') {
    manifest {
        attributes('Implementation-Title': project.name,
                'Implementation-Version': project.version)
    }
}

java {
    withSourcesJar()
    withJavadocJar()
}

ext.isReleaseVersion = !version.endsWith("SNAPSHOT")

tasks.withType(Sign) {
    onlyIf { isReleaseVersion }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId 'io.github.przybandrzej'
            artifactId 'yeelight-java-sdk'
            from components.java
            pom {
                name = 'Yeelight Java SDK'
                description = 'SDK for communicating with Yeelight devices'
                url = 'https://github.com/przybandrzej/yeelight-java-sdk'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/przybandrzej/yeelight-java-sdk.git'
                    developerConnection = 'scm:git:git@github.com:przyband/yeelight-java-sdk.git'
                    url = 'https://github.com/przybandrzej/yeelight-java-sdk'
                }

                developers {
                    developer {
                        id = 'przybandrzej'
                        name = 'Andrzej Przybysz'
                        email = 'andrzej.przybysz01@gmail.com'
                    }
                }
            }
        }
    }

    repositories {
        maven {
            def releaseRepo = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotRepo = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
            url = isReleaseVersion ? releaseRepo : snapshotRepo
            credentials {
                username = project.hasProperty('ossrhUsername') ? ossrhUsername : "Unknown user"
                password = project.hasProperty('ossrhPassword') ? ossrhPassword : "Unknown password"
            }
        }
    }
}

signing {
    required { isReleaseVersion && gradle.taskGraph.hasTask("publish") }
    sign publishing.publications.mavenJava
}
